# match dimlex:writtenRep against conll:WORD to retrieve lexical concept annotations 
# presuppose a lemon model has been loaded into the graph <http://www.w3.org/ns/lemon/ontolex> (ALWAYS!)
# create properties conll:LEX_ENTRY, conll:LEX_FORM, conll:LEX_SENSE, conll:LEX_CONCEPT, individually for every nif:Word
# note: presupposes untyped literals

PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX synsem: <http://www.w3.org/ns/lemon/synsem#>
PREFIX decomp: <http://www.w3.org/ns/lemon/decomp#>
PREFIX vartrans: <http://www.w3.org/ns/lemon/vartrans#>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xml: <http://www.w3.org/TR/xml/#>
prefix conll: <http://ufal.mff.cuni.cz/conll2009-st/task-description.html#>
prefix nif:   <http://persistence.uni-leipzig.org/nlp2rdf/ontologies/nif-core#>

# 1:1 matches only; note that we may generate more than one object per word
INSERT {
	?word conll:LEX_ENTRY ?entry; 
		  conll:LEX_FORM ?canonicalForm; 
		  conll:LEX_SENSE ?sense; 
		  conll:LEX_CONCEPT ?concept.
} WHERE {
	?word conll:WORD ?form.
	GRAPH <http://www.w3.org/ns/lemon/ontolex> {
		?entry (ontolex:canonicalForm|ontolex:otherForm|ontolex:lexicalForm)/ontolex:writtenRep ?form.
		OPTIONAL { ?entry ontolex:canonicalForm/ontolex:writtenRep ?canonicalForm }
		?entry ontolex:sense|^ontolex:isSenseOf ?sense.
		OPTIONAL { ?sense ontolex:isLexicalizedSenseOf|^ontolex:lexicalizedSense ?concept.}
	}
}