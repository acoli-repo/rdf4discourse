# given a "CoNLL" mapping between conll:SENSE and conll:PDTB3 in the default graph
# consult named graph <http://dimlex-lemon.org> that contains the corresponding dimlex lemon file

PREFIX powla: <http://purl.org/powla/powla.owl#> 
PREFIX conll: <http://ufal.mff.cuni.cz/conll2009-st/task-description.html#> 
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX terms: <http://purl.org/acoli/open-ie/> 
PREFIX x:     <http://purl.org/acoli/conll-rdf/xml#> 
PREFIX nif:   <http://persistence.uni-leipzig.org/nlp2rdf/ontologies/nif-core#> 
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#> 
PREFIX synsem: <http://www.w3.org/ns/lemon/synsem#> 
PREFIX decomp: <http://www.w3.org/ns/lemon/decomp#> 
PREFIX vartrans: <http://www.w3.org/ns/lemon/vartrans#> 
PREFIX lime: <http://www.w3.org/ns/lemon/lime#> 
PREFIX lexinfo: <https://www.lexinfo.net/ontology/2.0/lexinfo#>
PREFIX dimlex: <https://github.com/discourse-lab/dimlex/blob/master/DimLex.dtd#>
PREFIX discmar: <https://cs.famaf.unc.edu.ar/~laura/shallowdisc4summ/discmar/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# remove conll information
DELETE {
	?a a nif:Word; conll:ID ?id; conll:HEAD ?h; nif:nextWord ?n
} WHERE {
	?a a nif:Word; conll:HEAD ?h.
	OPTIONAL {?a conll:ID ?id }
	OPTIONAL { ?a nif:nextWord ?n}
};

DELETE {
	?a conll:SENSE ?sense; conll:PDTB3 ?pdtb3.
} INSERT {
	?w dimlex:form ?form ; dimlex:sense ?pdtb3
} WHERE {
	?a conll:SENSE ?sense; conll:PDTB3 ?pdtb3 .
	GRAPH <http://dimlex-lemon.org> {
		?s dimlex:sense ?sense; 
		   ontolex:isSenseOf ?w.
		?w (ontolex:canonicalForm|ontolex:lexicalForm|ontolex:otherForm)/ontolex:writtenRep ?form
	}
};